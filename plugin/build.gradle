apply plugin: 'groovy'

repositories { jcenter() }

dependencies {
    compileOnly gradleApi()

    // Checkstyle
    implementation('com.puppycrawl.tools:checkstyle:10.12.7') {
        // Android Lint also depends on guava, so don't bring it in twice
        exclude module: 'guava'
    }
    // SpotBugs (replaces FindBugs)
    implementation 'com.github.spotbugs:spotbugs:4.8.3'
    implementation 'com.github.spotbugs:spotbugs-ant:4.8.3'
    implementation 'com.github.spotbugs:spotbugs-annotations:4.8.3'
    // PMD
    implementation 'net.sourceforge.pmd:pmd-java:6.55.0'
    implementation 'net.sourceforge.pmd:pmd-core:6.55.0'
    // Error Prone
    implementation 'com.google.errorprone:error_prone_ant:2.26.1'
    // Detekt
    implementation 'io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.23.6'
    // Ktlint
    implementation 'org.jlleitschuh.gradle:ktlint-gradle:12.1.0'
}

project.ext {
    plugin = 'pt.simdea.verifier'
    name = 'Android Quality Verifier Plugin'
    description = 'Static code analysis plugin for Android projects.'
    groupId = 'pt.simdea.verifier'
    artifactId = 'verifier'
    version = "4.0.0-alpha1"
    website = 'https://github.com/simdea/android-quality-verifier'
    scm = 'https://github.com/simdea/android-quality-verifier'
    tags = ['android', 'verifier', 'check', 'checkstyle', 'spotbugs', 'pmd', 'lint', 'ktlint', 'detekt', 'error prone', 'quality']
    pom = {
        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'https://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }
        scm {
            url project.ext.scm
            connection 'scm:git@github.com:simdea/android-quality-verifier.git'
            developerConnection 'scm:git@github.com:simdea/android-quality-verifier.git'
        }
        issueManagement {
            system 'GitHub'
            url "${project.ext.scm}/issues"
        }
        developers {
            developer {
                name 'Simdea'
                url 'https://www.simdea.pt'
                roles {
                    role 'developer'
                }
            }
        }
    }
    bintray = {
        user = project.hasProperty('bintrayUser') ? project['bintrayUser'] : 'anonymous'
        key = project.hasProperty('bintrayKey') ? project['bintrayKey'] : 'api-key'
        repo = 'android-quality-verifier'
        org = 'simdea'
        licenses = ['MIT']
        issueTracker = "${project.ext.scm}/issues"
        vcs = project.ext.scm
        sign = true
        dryRun = false
        publish = true
    }
}

apply from: project.file('gradle/gradle-publish.gradle')
apply from: project.file('gradle/publish.gradle')